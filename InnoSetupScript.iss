; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;change this folder to use a diffrent qt-version
#define QtSourceDir = "C:\Qt\5.6\msvc2015"
#define QtBuildDir = "E:\Projekte\build-MinetestMapperGui-Desktop_Qt_5_6_1_MSVC2015_32bit-Release\release"
#define MtMapperDir = "C:\Users\Adrian\Documents\minetest-mapper-cpp"

#define MyAppName "Minetest Mapper Gui"
#define MyAppVersion GetFileVersion(QtBuildDir +'\MinetestMapperGui.exe')
#define MyAppPublisher "adrido"
#define MyAppURL "https://bitbucket.org/adrido/minetestmappergui"
#define MyAppExeName "MinetestMapperGui.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{B51924E7-DBDF-4329-BBFA-AB842493474E}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputBaseFilename=Setup {#MyAppName} x32
MinVersion=0,5.01sp3
UninstallDisplayIcon={app}\MinetestMapperGui.exe
WizardSmallImageFile=.\images\WizardSmallImage.bmp
WizardImageFile=.\images\WizardImage.bmp
InfoBeforeFile=.\LICENSE
VersionInfoDescription={#MyAppName} Setup
VersionInfoVersion={#MyAppVersion}
VersionInfoTextVersion={#MyAppVersion}
VersionInfoProductName={#MyAppName}
VersionInfoProductVersion={#MyAppVersion}
VersionInfoCopyright=2016 adrido; CC BY 3.0
AllowNoIcons=True
Compression=lzma2/ultra64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\MinetestMapperGui.exe"; IconIndex: 0
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\MinetestMapperGui.exe"; IconIndex: 0; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Flags: nowait postinstall skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"
Filename: "{tmp}\vc_redist.x86.exe"; Parameters: "/install /passive /norestart"; Flags: 32bit; Description: "Microsoft Visual C++ 2015 Redistributable Update 2"; StatusMsg: "Installing Microsoft Visual C++ 2015 Redistributable Update 2"

[Components]
Name: "minetestmappergui"; Description: "Minetest Mapper GUI (required)"; Types: full compact custom; Flags: fixed
Name: "minetestmapper_cmd"; Description: "Minetestmapper by Rogier 5 (Recommended)"; Types: full compact custom; Flags: checkablealone
Name: "color_txt_files"; Description: "colors.txt files (Recommended)"; Types: full; Flags: checkablealone

[Files]
Source: "{#MtMapperDir}\Release\libgd.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: minetestmapper_cmd
Source: "{#MtMapperDir}\Release\libpng16.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: minetestmapper_cmd
Source: "{#MtMapperDir}\Release\sqlite3.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: minetestmapper_cmd
Source: "{#MtMapperDir}\Release\minetestmapper.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: minetestmapper_cmd
Source: "{#MtMapperDir}\Release\zlib.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: minetestmapper_cmd

Source: "{#QtSourceDir}\bin\icudt54.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\icuin54.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\icuuc54.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\libEGL.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtBuildDir}\MinetestMapperGui.exe"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui

Source: "{#QtSourceDir}\bin\Qt5Core.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\Qt5Gui.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\Qt5Svg.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\Qt5Widgets.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui
Source: "{#QtSourceDir}\bin\Qt5WinExtras.dll"; DestDir: "{app}"; Flags: ignoreversion 32bit; Components: minetestmappergui

Source: "{#MtMapperDir}\colors-average-alpha.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full
Source: "{#MtMapperDir}\colors-cumulative-alpha.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full
Source: "{#MtMapperDir}\colors.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full
Source: "{#MtMapperDir}\heightmap-colors-rainbow.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full
Source: "{#MtMapperDir}\heightmap-colors.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full
Source: "{#MtMapperDir}\heightmap-nodes.txt"; DestDir: "{app}\colors\"; Flags: ignoreversion; Components: color_txt_files; Permissions: everyone-full

Source: ".\languages\de.png"; DestDir: "{app}\languages\"; Flags: ignoreversion
Source: ".\languages\en.png"; DestDir: "{app}\languages\"; Flags: ignoreversion
Source: ".\languages\gui_de.qm"; DestDir: "{app}\languages\"; Flags: ignoreversion
Source: ".\languages\gui_en.qm"; DestDir: "{app}\languages\"; Flags: ignoreversion
Source: "{#QtSourceDir}\translations\qtbase_de.qm"; DestDir: "{app}\languages\"; Flags: ignoreversion

Source: "{#QtSourceDir}\plugins\iconengines\qsvgicon.dll"; DestDir: "{app}\plugins\iconengines\"; Flags: ignoreversion; Components: minetestmappergui
Source: "{#QtSourceDir}\plugins\imageformats\qsvg.dll"; DestDir: "{app}\plugins\imageformats\"; Flags: ignoreversion; Components: minetestmappergui
Source: "{#QtSourceDir}\plugins\platforms\qwindows.dll"; DestDir: "{app}\plugins\platforms\"; Flags: ignoreversion; Components: minetestmappergui
Source: "..\..\vc_redist.x86.exe"; DestDir: "{tmp}"; Flags: 32bit deleteafterinstall

[Dirs]
Name: "{app}\colors\colors"; Components: color_txt_files
Name: "{app}\plugins\iconengines\"
Name: "{app}\plugins\imageformats\"
Name: "{app}\plugins\platforms\"

[INI]
;create qt.conf file and specify the plugins path
Filename: "{app}\qt.conf"; Section: "Paths"; Key: "Plugins"; String: "plugins";

[UninstallDelete]
Type: files; Name: "{app}\qt.conf"
Type: dirifempty; Name: "{app}"

